## To run this workflow:
## First, generate `usher/trimmed/usher_translations.tsv` using matUtils summary --translate
## Then run analysis in main directory using `snakemake -s code/dNdS_snakefile`

rule all:
    input:
        diffs = "usher/trimmed/dn_ds_dstop_bootstrap.tsv",
        diffs_degen = "usher/trimmed/dn_ds_dstop_bootstrap_4degenerate.tsv"

rule setup_bootstrap:
    input:
        muts = "usher/trimmed/usher_translations.tsv",
        tree = "usher/trimmed/trimmed_2023-05-01.all.nwk",
        dates = "usher/public-2023-05-01.metadata.tsv.gz"
    output:
        muts = "usher/trimmed/usher_mutTypes.tsv",
    shell:
        '''
        python code/setup_bootstrap.py \
            --muts {input.muts} \
            --tree {input.tree} \
            --dates {input.dates} \
            --output {output.muts}
        '''

rule run_bootstrap:
    input:
        muts = "usher/trimmed/usher_mutTypes.tsv",
    params:
        iterations = 10
    output:
        counts = "usher/trimmed/bootstrap/counts{iteration}.tsv",
    shell:
        '''
        python code/run_bootstrap.py \
            --muts {input.muts} \
            --counts {output.counts} \
            --size {params.iterations}
        '''

chunks = list(range(1000))

rule combine_bootstrap:
    input:
        ref = "params/sars2_wS1_ref.gb",
        muts = "usher/trimmed/usher_mutTypes.tsv",
        counts = expand("usher/trimmed/bootstrap/counts{iteration}.tsv", iteration=chunks)
    params:
        ci = 95
    output:
        diffs = "usher/trimmed/dn_ds_dstop_bootstrap.tsv"
    shell:
        '''
        python code/combine_bootstrap.py \
            --ref {input.ref} \
            --muts {input.muts} \
            --ci {params.ci} \
            --counts {input.counts} \
            --diffs {output.diffs}
        '''

rule combine_bootstrap_degen:
    input:
        ref = "params/sars2_wS1_ref.gb",
        muts = "usher/trimmed/usher_mutTypes.tsv",
        counts = expand("usher/trimmed/bootstrap/counts{iteration}.tsv", iteration=chunks)
    params:
        ci = 95
    output:
        diffs = "usher/trimmed/dn_ds_dstop_bootstrap_4degenerate.tsv"
    shell:
        '''
        python code/combine_bootstrap.py \
            --ref {input.ref} \
            --muts {input.muts} \
            --ci {params.ci} \
            --counts {input.counts} \
            --degenerate \
            --diffs {output.diffs}
        '''
